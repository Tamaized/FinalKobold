buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://maven.parchmentmc.org' }
        jcenter()
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply from: 'https://raw.githubusercontent.com/SizableShrimp/Forge-Class-Remapper/main/classremapper.gradle'

repositories {
    maven {
        url = "https://dvs1.progwml6.com/files/maven/" // JEI
    }
    maven {
        url = "https://modmaven.dev/" // TF
    }
}

version = "1.${minecraftVersionMajor}-${modVersion}${modVersionValue}"
group = "tamaized.fk"
archivesBaseName = "FinalKobold"
java.toolchain.languageVersion = JavaLanguageVersion.of(16)

minecraft {
    mappings channel: 'parchment', version: '2021.10.31-1.17.1'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                melongolem {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                melongolem {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.${project.minecraftVersionMajor}.${project.minecraftVersionMinor}-${project.forgeVersion}"

    runtimeOnly fg.deobf("mezz.jei:${project.jeiVersion}")
    implementation fg.deobf("teamtwilight:twilightforest:${project.tfVersion}:universal")
}

processResources {
    filesMatching("META-INF/mods.toml") {
        expand([
                'version': "${project.version}"
        ])
    }
}

jar {
    manifest {
        attributes(["Specification-Title"     : "FinalKobold",
                    "Specification-Vendor"    : "Tamaized",
                    "Specification-Version"   : "${modVersion}${modVersionValue}",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${version}",
                    "Implementation-Vendor"   : "Tamaized",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

task finalize doLast {
    ant.propertyfile(file: "gradle.properties") {
        entry(key: "modVersionValue", value: Integer.parseInt(project.modVersionValue) + 1)
    }
}

build.finalizedBy(finalize)